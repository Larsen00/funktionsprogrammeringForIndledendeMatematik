\section{PBT af programmet}
Vi skal nu validere vores program ved hjælp af PBT, ved at lave funktioner, som kan undersøge de egenskaber der løbende er blevet beskrevet i rapporten. 

\subsection{PBT af udtryk}
Før vi begynder at udføre PBT på alle egenskaberne vedrørende udtryk, skal vi først bygge en generator for vores talmængde og udtryk. Da vi ikke altid kan sammenligne, om to udtrykstræer er ækvivalente, vil vi i alle vores PBT, hvor vi ønsker at sammenligne træer, gøre dette ved brug af et miljø, som indeholder værdier for variablene, og derefter evaluere udtrykene ved brug af \textcolor{red}{eval}-funktionen.

Vi begynder med at definere en række generatorer, som kan generere blade i vores udtrykstræer i Listing \ref{generator1}.


\begin{lstlisting}[
    language={FSharp}, 
    label={generator1}, 
    caption={Generatorene anvendt til PBT af udtryk}
    ]
let max = 3
let min = -3

// noneZeroGen: Gen<int>
let noneZeroGen = 
    Gen.oneof [ 
        Gen.choose(1, max) ;
        Gen.choose(min, -1)]

// numberGen: Gen<Number>
let numberGen =
    Gen.oneof [
        Gen.map2 (fun x y -> newRational(x, y) |> Rational |> tryReduce ) (Gen.choose(min, max)) noneZeroGen;
        Gen.map (fun x -> Int x) (Gen.choose(min, max));
        Gen.map4 (fun a b c d -> newComplex (newRational(a, b), newRational(c, d)) |> Complex |> tryReduce ) (Gen.choose(min, max)) noneZeroGen (Gen.choose(min, max)) noneZeroGen]

// numberInExprGen: Gen<Expr<Number>>
let numberInExprGen = 
    Gen.map (fun x -> N x) numberGen

// randomListElement: list<'a> -> Gen<'a>
let randomListElement xlist = 
    gen { let! i = Gen.choose(0, List.length xlist - 1)
        return xlist.[i] }

// variableGen: list<char> -> Gen<Expr<'a>>
let variableGen xlist = Gen.map X (randomListElement xlist)

// leafGen: list<char> -> Gen<Expr<Number>>
let leafGen xlist =
    if xlist <> [] then
        Gen.oneof [numberInExprGen; variableGen xlist]
    else
        numberInExprGen

// onlyIntleafGen: list<char> -> Gen<Expr<Number>>
let onlyIntleafGen xlist :Gen<Expr<Number>> = 
    if xlist <> [] then
        Gen.oneof [Gen.map (fun x -> N <| Int x) (Gen.choose(-10, 10)); variableGen xlist]
    else
        Gen.map (fun x -> N <| Int x) (Gen.choose(-10, 10))

// charsSeqGen: char -> char -> seq<Gen<char>>
let charsSeqGen c1 c2 = seq { for c in c1 .. c2 do
                                yield gen { return c} }

// charGen: Gen<char>
let charGen = gen { return! Gen.oneof (charsSeqGen 'A' 'Z')}

// smallEnvGen: Gen<Map<char, Number> * list<char>>
let smallEnvGen =
    gen { 
        let! i = Gen.choose (0, 5)
        let! xlist = Gen.listOfLength i charGen
        let! ns = Gen.listOfLength i numberGen
        return (Map.ofList (List.zip xlist ns), xlist) }

// exprGen: 'a -> int -> ('a -> Gen<Expr<'b>>) -> Gen<Expr<'b>> 
let rec exprGen xlist n leafType = 
    if n = 0 then
        leafType xlist
    else
        Gen.oneof [
            // leaf occurs twice becourse leaf is X or N giving the same probability for each expression 
            leafType xlist; 
            leafType xlist;
            Gen.map2 (fun x y -> Add (x, y)) (exprGen xlist (n/2) leafType) (exprGen xlist (n/2) leafType);
            Gen.map2 (fun x y -> Mul (x, y)) (exprGen xlist (n/2) leafType) (exprGen xlist (n/2) leafType);
            Gen.map2 (fun x y -> Div (x, y)) (exprGen xlist (n/2) leafType) (exprGen xlist (n/2) leafType);
            Gen.map2 (fun x y -> Sub (x, y)) (exprGen xlist (n/2) leafType) (exprGen xlist (n/2) leafType);            
            Gen.map (fun x -> Neg x) (exprGen xlist (n/2) leafType)]


type SmallEnv = Map<char, Number> * char list
type SmallEnvGen =
    static member SmallEnv() =
        {new Arbitrary<SmallEnv>() with
            override _.Generator = smallEnvGen
            override _.Shrinker _ = Seq.empty}

type NumberGen =
static member Number() =
    {new Arbitrary<Number>() with
        override _.Generator = numberGen
        override _.Shrinker _ = Seq.empty}    
\end{lstlisting}

Først defineres to variable, som alle funktioner har til rådighed, \texttt{max} og \texttt{min}, som sætter intervallet for de heltal, der kan anvendes i genereringen af Numbers. 
Den første funktion, \textcolor{red}{noneZeroGen}, er en generator for et tilfældigt heltal fra sættet $S_1$:
\begin{gather*}
    S_1 = \{ x \mid x \in \mathbb{Z} \setminus \{0\}, \quad \texttt{min} \leq x \leq \texttt{max} \}.
\end{gather*}
Vi kan dermed bruge \textcolor{red}{noneZeroGen}, da vi ikke ønsker at generere rationale tal med nævneren 0. For at definere en generator for Numbers i form af \textcolor{red}{numberGen}, som genererer et gyldigt Number fra sættet $S_5$:
\begin{align*}
    S_2 &= \left\{ x \mid x \in \mathbb{Z}, \quad \texttt{min} \leq x \leq \texttt{max} \right\} \\
    S_3 &= \left\{ \frac{x}{y} \mid x \in S_1, y \in S_2 \right\} \\
    S_4 &= \left\{ x + yi \mid x, y \in S_3 \right\} \\
    S_5 &= S_2 \cup S_3 \cup S_4
\end{align*}

Funktionen \textcolor{red}{numberInExprGen} er en generator, som ved brug af \textcolor{red}{numberGen} konverterer et \texttt{Number} til et \texttt{Expr<Number>}. Det er disse generatorer, der anvendes til at generere tal til vores talmængde.

Dernæst kommer nogle generatorer til generering af variable. Først har vi \textcolor{red}{randomListElement}, som tager en liste af en vilkårlig type og udvælger et tilfældigt element fra listen. Derudover har vi \textcolor{red}{variableGen}, som tager en liste af karakterer og bruger \textcolor{red}{randomListElement} til at udvælge en af karaktererne og konvertere den til et \texttt{Expr<Number>}.

Dermed er det nu muligt at lave en generator, som kan generere enten et tal eller en variabel fra konstruktørerne af \texttt{Expr<Number>}. Disse to konstruktører er også bladene i vores udtrykstræer. \textcolor{red}{leafGen} genererer et tilfældigt blad i vores udtrykstræ ud fra en liste af karakterer. \textcolor{red}{onlyIntleafGen} fungerer ud fra det samme princip, men genererer kun heltal fra intervallet -10 til 10.

Funktionerne \textcolor{red}{charGen} og \textcolor{red}{charsSeqGen} er generatorer, som sammen genererer en tilfældig karakter mellem 'A' og 'Z'. Endelig har vi en miljøgenerator, \textcolor{red}{smallEnvGen}, som genererer et par af et miljø samt liste af variable i miljøet. Miljøet indeholder variable og deres tilsvarende værdier.

Den sidste generator, \textcolor{red}{exprGen}, vil i vores PBT tage en liste af variable, som den må generere blade ud fra, samt hvilken generator der skal anvendes til at generere bladene. Derudover vil den maksimale dybde af udtrykket være \(\log_2(n)\).

Til sidst defineres tre typer, hvor \texttt{SmallEnvGen} og \texttt{NumberGen} gør det muligt inden kørsel af vores PBT at registrere generatorerne.

Vi har dermed nu lavet fundamentet, til at kunne teste vores egenskaber vedrørende udtryk.

\subsubsection{Tal modulet}\label{sec:PBT_number}
De 6 egenskaber fra \ref{egenskab:tal} kan nu testes med PBT. Egenskaberne kan oversættes direkte til funktioner, grundet de overskrivninger vi har lavet på number typen i Listing \ref{pbt:number}. 

\lstinputlisting[
    language=FSharp, 
    caption={\textit{numberPBT.fsx} - funktioner til test af egenskaberne i \ref{egenskab:tal}},
    label={pbt:number}
    ]{../PBT/numberPBT.fsx}
I \textcolor{red}{inverseMultiplicative} anvender vi "Prop.classify" til at tillade, at egenskaben kan slå bestemte fejl. I dette tilfælde tillades det, at der opstår en "DivideByZeroException". Dette sker, fordi generatoren godt kan generere 0, som der ikke kan tages en invers af. For at teste ovensånde funktioner, køres \textit{.fsx} filen, og outputtet kan ses i Listing \ref{output:number}.
\begin{lstlisting}[
    style=output, 
    label={output:number}, 
    caption={Outputtet fra PBT af \texttt{Number} typer, ved kørsel af Listing \ref{pbt:number}}
    ]        
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
94% PropertyHolds.
6% DivideByZeroExceptions.
\end{lstlisting}

Dermed viser testen at alle egenskaberne fra \ref{egenskab:tal} holder. 

\subsubsection{Homomorfisme af evaluering}\label{sec:PBT_eval_homomorphism}

Vi skal nu teste egenskaben fra \ref{prop:eval_homomorphism}. Testen er lavet i Listing \ref{pbt:eval_homomorphism}. Dette er gjort ved, at der genereres et tilfældigt miljø, hvori der udstrækkes to udtryk ved brug af udtryks generatoren, hvori variablene er indeholdt i miljøet. For alle operatorer testes det, om de overholder egenskaben om homomorfisme.


\begin{lstlisting}[
    language={FSharp}, 
    label={pbt:eval_homomorphism}, 
    caption={PBT af egenskaben omkring Homomorfisme fra \ref{prop:eval_homomorphism}}
    ]
// evalOperation: Expr<Number> -> Expr<Number> -> Map<char, Number> -> (Expr<Number> -> Expr<Number> -> Expr<Number>) -> bool
let evalOperation e1 e2 env f =
eval (f e1 e2) env = (getNumber <| f (eval e1 env |> N) (eval e2 env |> N))
 
let evalPBT ((env ,xlist):SmallEnv) = 
    let result = 
        try
            let exprList = Gen.sample 1 2 (exprGen xlist 10 leafGen)
            let e1::[e2] = exprList
            let prop = evalOperation e1 e2 env
            let negation = eval (-e1) env = - eval e1 env
            if negation && prop ( + ) && prop ( - ) && prop ( * ) && prop ( / ) then 1 else 0
        with
        | :? System.DivideByZeroException as _ -> 2
        | :? System.OverflowException as _ -> 3
    (result = 1 || result = 2 || result = 3)
    |> Prop.classify (result = 1) "Property Holds"
    |> Prop.classify (result = 2) "DivideByZeroExceptions"
    |> Prop.classify (result = 3) "OverflowException"    
\end{lstlisting}

Outputtet fra testen kan ses i Listing \ref{output:eval_homomorphism}. Testen viser, at egenskaben holder for alle operationer.

\begin{lstlisting}[
    style=output, 
    label={output:eval_homomorphism}, 
    caption={Outputtet fra PBT af egenskaben \ref{prop:eval_homomorphism}}
    ]
> Arb.register<SmallEnvGen>()
- let _ = Check.Quick evalPBT;;
Ok, passed 100 tests.
76% Property Holds.
18% DivideByZeroExceptions.
6% OverflowException.
\end{lstlisting}
Vi ser her en større mængde af tests, som bliver klassificeret som "DivideByZeroExceptions". Dette skyldes, at vores generator godt kan generere udtryk som \(1/(0 \cdot X)\) og lignende, hvilket ikke er et lovligt udtryk. Derudover forekommer der også "OverflowExceptions", hvilket skyldes, at ved evaluering af udtryk kan vi godt ende med en kombination af operationer, som giver rationale tal, der, som beskrevet i sektion \ref{sec:rational}, kan resultere i en overflow. Samme form for klassificeringer vil vi løbende se i de kommende tests.



\subsubsection{Simplifikation af udtryk}\label{sec:PBT_simplification}
Det er vigtigt, at vores simplifikation altid er lig med det oprindelige udtryk. Vi kan derfor teste egenskab \ref{egenskab:simplification} ved at simplificere et udtryk og sammenligne evalueringen af det med det oprindelige udtryk ved hjælp af et miljø. Dette er gjort i Listing \ref{pbt:simplification}.

\begin{lstlisting}[language={FSharp}, caption={PBT af egenskaben \ref{egenskab:simplification}}, label={pbt:simplification}]
// compareSimpExpr: Map<char, Number> -> Expr<Number> -> bool
let compareSimpExpr env (e:Expr<Number>) =
    eval (simplifyExpr e) env = eval e env

// simpEqualEval: SmallEnv -> int
let simpEqualEval (env, xlist) = 
    try
        if Gen.sample 1 1 (exprGen xlist 10 leafGen) 
            |> List.head 
            |> compareSimpExpr env 
        then 1 else 0
    with
        | :? System.DivideByZeroException as _ -> 2
        | :? System.OverflowException as _ -> 3

// simpPBT: SmallEnv -> Property
let simpPBT (se:SmallEnv) =
    let result = simpEqualEval se
    (result = 1 || result = 2 || result = 3)
    |> Prop.classify (result = 1) "Equal"
    |> Prop.classify (result = 2) "DivideByZeroExceptions"
    |> Prop.classify (result = 3) "OverflowException"   
\end{lstlisting}

Outputtet fra testen kan ses i Listing \ref{output:simplification}. Testen viser, at egenskaben holder.

\begin{lstlisting}[
    style=output, 
    label={output:simplification}, 
    caption={Outputtet fra PBT af egenskaben \ref{egenskab:simplification}}
    ]
> Arb.register<SmallEnvGen>()
- let _ = Check.Quick simpPBT;;
Ok, passed 100 tests.
94% Equal.
6% DivideByZeroExceptions.
\end{lstlisting}

\subsubsection{Invers morfisme mellem infix og prefix}\label{sec:PBT_infix_prefix}
Nu skal det undersøges, hvorvidt egenskab \ref{egenskab:infix_prefix} holder. Vi beskrev i Listing \ref{lst:expression_to_infix} den inverse funktion til \textcolor{red}{tree}-funktionen, som benyttes til at opskrive egenskaben i Listing \ref{pbt:infix_prefix}.

\begin{lstlisting}[language={FSharp}, caption={PBT af egenskaben \ref{egenskab:infix_prefix}}, label={pbt:infix_prefix}]
// generatesCorrectTree: Map<char, Number> -> Expr<Number> -> bool
let generatesCorrectTree env (e:Expr<Number>) =
    eval e env = eval 
        (simplifyExpr e 
        |> infixExpression 
        |> tree 
        |> infixExpression 
        |> tree ) env

// treeEqualEval: SmallEnv -> int
let treeEqualEval (env, xlist) =
    try 
        if Gen.sample 1 1 (exprGen xlist 10 onlyIntleafGen) 
            |> List.head 
            |> generatesCorrectTree env 
        then 1 else 0
    with
        | :? System.DivideByZeroException as _ -> 2
        | :? System.OverflowException as _ -> 3

// treePBT: SmallEnv -> Property
let treePBT (se:SmallEnv) =
    let result = treeEqualEval se
    (result = 1 || result = 2 || result = 3)
    |> Prop.classify (result = 1) "Equal"
    |> Prop.classify (result = 2) "DivideByZeroExceptions"
    |> Prop.classify (result = 3) "OverflowException"
\end{lstlisting}

Funktionen \textcolor{red}{generatesCorrectTree} tager et miljø samt et udtryk og undersøger, om:
\begin{gather}
    \text{eval}(e) = \text{eval}(\text{tree}(\text{tree}^{-1}(\text{tree}(\text{tree}^{-1}(\text{simplifyExpr}(e))))))
\end{gather}
Det er gjort på denne måde, da det ikke er muligt igennem vores program at generere matematiske udtryk i infix-notation, som med sikkerhed har gyldig notation. I stedet vil vi anvende vores udtryksgenerator til at generere et udtryk, som vi så ved hjælp af et miljø kan tjekke, at egenskaben \ref{egenskab:infix_prefix} holder for alle \( x \in \texttt{Expr<Number>} \). 

Udtrykket bliver simplificeret, før det konverteres til infix-notation, da der er valgt ikke at placere parenteser rundt om tal i udtrykket, for visualiseringens skyld. Dette betyder, at hvis udtrykket ikke simplificeres, kan der skabes udtryk som \texttt{Neg(N -1)}, som i infix-notation er \(--1\), hvilket ikke er en gyldig notation uden parenteser. Simplifikationen sørger for, at negative tal repræsenteres ved negationen af et positivt tal.
 

Outputtet fra kørsel af testen kan ses i Listing \ref{output:infix_prefix}. Testen viser som forventet, at egenskaben holder for alle udtryk.
\begin{lstlisting}[
    style=output, 
    label={output:infix_prefix}, 
    caption={Outputtet fra PBT af egenskaben \ref{egenskab:infix_prefix}}
    ]
> Arb.register<SmallEnvGen>()                               
- let _ = Check.Quick treePBT;;
Ok, passed 100 tests.
94% Equal.
6% DivideByZeroExceptions.
\end{lstlisting}


\subsubsection{Differentiering af udtryk}\label{sec:PBT_differentiation}
Selv om differentieringsfunktionen \textcolor{red}{diff} er implementeret ud fra egenskaberne i \ref{egenskab:differentiation}, er det stadig vigtigt at teste, om funktionen overholder de samme egenskaber. Dette er gjort i \textit{diffPBT.fsx}, se Listing \ref{pbt:differentiation}.

\lstinputlisting[language=FSharp, caption={\textit{diffPBT.fsx} - funktioner til test af egenskaberne i \ref{egenskab:differentiation}}, label={pbt:differentiation}]{../PBT/diffPBT.fsx}

Outputtet fra kørsel af testen kan ses i Listing \ref{output:differentiation}. Testen viser, at egenskaberne holder.

\begin{lstlisting}[style=output, caption={Outputtet fra PBT af differentiering af udtryk}, label={output:differentiation}]
Differentiation property based testing
Ok, passed 100 tests.
72% Property Holds.
15% DivideByZeroExceptions.
13% OverflowException.
\end{lstlisting}



\subsection{PBT af vektorer og matricer}
Ligesom ved PBT af udtryk begynder vi med at lave end generatorer for matricer.

\begin{lstlisting}[
    language={FSharp}, 
    label={generators}, 
    caption={Generatorerne anvendt til PBT af matrixoperationer}
    ]
// vectorGen : int -> Gen<Vector>
let vectorGen n =
    Gen.listOfLength n numberGen |> Gen.map (fun x -> vector x)

// matrixGen : Gen<Matrix>
let matrixGen =
    gen {
        let! row = Gen.choose(1, 6)
        let! col = Gen.choose(1, 6)
        let! vectors = Gen.listOfLength col (vectorGen row)
        return matrix vectors
    }

type MatrixGen =
    static member Matrix() =
        {new Arbitrary<Matrix>() with
            override _.Generator = matrixGen
            override _.Shrinker _ = Seq.empty}
\end{lstlisting}

Først defineres \textcolor{red}{vectorGen}, som laver en liste ved hjælp af den tidligere definerede funktion \textcolor{red}{numberGen}. Den laver en liste med en given længde af tilfældige tal fra vores talmængde. Dernæst passes listen videre til \textcolor{red}{vector}-funktionen, som laver en søjlelagret vektor ud fra listen. Funktionen \textcolor{red}{matrixGen} genererer en matrix af tilfældig størrelse, hvor antallet af rækker og kolonner er mellem 1 og 6 ved hjælp af \textcolor{red}{vectorGen}. Til sidst kan vi definere en type for matrixgeneratorerne, som gør det muligt at registrere dem før kørsel af PBT.

\subsubsection{PBT af matrix operationer}
Det er nu muligt at opstille nogle PBT af der sikre at matricerne overholder matematiske egenskaber i sætning \ref{vector_space_axioms}. 




Vi kan dermed nu lave definere egenskaberne fra \ref{vector_space_axioms} som nogle funktioner i Listing \ref{lst:vector_space_axioms}.

\begin{lstlisting}[
    language={FSharp}, 
    label={lst:vector_space_axioms}, 
    caption={Egenskaberne fra sætning \ref{vector_space_axioms} som funktioner}
    ]
//vectorCom : Matrix -> bool
let vectorCom m =
    sumRows m = sumRows (flip m)

//vectorScalarAss : Matrix -> Number -> Number -> bool
let vectorScalarAss (m:Matrix) (n1:Number) (n2:Number) =
    n1 * (n2 * m) = (n1 * n2) * m

//vectorAssCom : Matrix -> Number -> bool
let vectorAssCom m (c:Number) =
    c * (sumRows m) = sumRows (c * m)
\end{lstlisting}

Listing \ref{lst:vector_space_axioms_pbt} viser outputtet fra kørsel af testene. Testene viser, at egenskaberne holder for alle matricer.

\begin{lstlisting}[
    style=output, 
    label={lst:vector_space_axioms_pbt}, 
    caption={Outputtet fra PBT af vektor Listing \ref{lst:vector_space_axioms}}
    ]
> Arb.register<MaxtrixGen>()
- let _ = Check.Quick vectorCom
- let _ = Check.Quick vectorScalarAss
- let _ = Check.Quick vectorAssCom;;
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
\end{lstlisting}

\subsubsection{PBT af Gram-Schmidt}\label{sec:pbt_gram_schmidt}
Udfordringen ved at lave en PBT af Gram-Schmidt er, at vektorsættet skal være lineært uafhængigt. Derfor laves der en generator, som ved at udføre tilfældige rækkeoperationer på en diagonal matrix med \( m \) rækker og \( n \) søjler, hvorom det gælder, at \( n \geq m \). Dette sikrer os, at rækkerne i matricen er lineært uafhængige. Hertil vil den transponerede matrix have lineært uafhængige søjler, som vi kan bruge til at teste Gram-Schmidt processen\footcitetitle{rank}. Listing \ref{generators_gram_schmidt} viser de forskellige generatorer, som anvendes til PBT af Gram-Schmidt-processen.

\begin{lstlisting}[
    language={FSharp}, 
    label={generators_gram_schmidt}, 
    caption={Generatorene anvendt til PBT af Gram-Schmidt}
    ]
// performRowOperationGen: Matrix -> Gen<Matrix>
let performRowOperationGen m =
    let (D(n, _)) = dimMatrix m
    gen { 
        let! i = Gen.choose(1, n)
        let! j = match i with
                    | 1 -> Gen.choose(2, n)
                    | _ when i = n -> Gen.choose(1, n-1)
                    | _ -> Gen.oneof [Gen.choose(1, i-1); Gen.choose(i+1, n)]
        let! a = numberGen
        return rowOperation i j a m }

// multipleRowOperationsGen: Matrix -> int -> Gen<Matrix>
let rec multipleRowOperationsGen m count =
    if count <= 0 then Gen.constant m
    else
        gen {
            let! newMatrix = performRowOperationGen m
            return! multipleRowOperationsGen newMatrix (count - 1)
        }

// getDiagonalMatrixGen: int -> Gen<Matrix>
let getDiagonalMatrixGen maxRows =
    gen { 
        let! m = Gen.choose(2, maxRows)
        let! n = Gen.choose(m, m + 3)   
        return fullrankedDiagonalMatrix m n }
        
// getIndependentBasisGen: Gen<Matrix>
let getIndependentBasisGen =
    gen { 
        let! A = getDiagonalMatrixGen 5
        let! numberOfOperations = Gen.choose(1, 10)
        let! span = multipleRowOperationsGen A numberOfOperations
        return span |> transposeMatrix  }

type IndependentBasisMatrix = Matrix
type IndependentBasisMatrixGen =
    static member IndependentBasisMatrix() =
        {new Arbitrary<Matrix>() with 
            override _.Generator = getIndependentBasisGen
            override _.Shrinker _ = Seq.empty}
\end{lstlisting}

Funktionen \textcolor{red}{performRowOperationGen} tager en matrix, hvori der udvælges to tilfældige rækker, \(i\) og \(j\), hvorefter der udføres en rækkeoperation på \(R_j\), således at \(R_j \leftarrow R_j - aR_i\), hvor \(a\) er et tilfældigt tal. \textcolor{red}{multipleRowOperationsGen} gentager denne proces et givet antal gange. Funktionen \textcolor{red}{getDiagonalMatrixGen} laver en diagonal matrix med \(m\) rækker og \(n\) søjler, hvor \(n \geq m\). \textcolor{red}{getIndependentBasisGen} genererer en matrix, hvis søjler er lineært uafhængige. Vi kan dermed definere en funktionen til at undersøge om en matrix er en ortogonal basis, som vist i Listing \ref{check_orthogonal_basis}.

\begin{lstlisting}[
    language={FSharp}, 
    label={check_orthogonal_basis}, 
    caption={Funktion til at tjekke om søjlerne i en matrix er en ortogonal basis}
    ]
// isOrthogonalBacis : Matrix -> bool
let rec isOrthogonalBacis m =
    (transposeMatrix m |> conjugateMatrix) * m |> isDiagonalMatrix
\end{lstlisting}



Dernæst skal vi bruge en funktion til at tjekke, om en matrix er en ortogonal basis. \textcolor{red}{isOrthogonalBacis} i Listing \ref{check_orthogonal_basis} tjekker, om alle vektorerne i en matrix er ortogonale med hinanden. Dette kan gøres ved hjælp af følgende sætning:

\vspace{0.5cm}
\begin{theorem}
    Lad \( A \) have søjlerne \( v_1, v_2, \ldots, v_n \), som udgør en ortogonal basis. Per definition gælder det, at det indre produkt mellem to ortogonale vektorer \( v_i \) og \( v_j \) er:
    \begin{gather}
        \langle v_i, v_j \rangle = 0 \quad \text{for} \quad i \neq j
    \end{gather}
    Lad så \( D \) være en kvadratisk diagonal matrix. Da gælder det, at:
    \begin{gather}
        D = A^* A
    \end{gather}
\begin{proof}
    Fra definitionen af matrixprodukt \ref{def:matrix_matrix_pro} ved vi, at:
    \begin{gather}
        A^*A = 
        \begin{bmatrix}
            | &  & | \\
            A^* \cdot v_1 & \cdots & A^* \cdot v_n \\
            | &  & | \\
        \end{bmatrix}
    \end{gather}
    Fra definitionen af matrix-vektorprodukt \ref{def:matrix_vector_pro} ved vi, at:
    \begin{gather}
        A^* v_i = 
        \begin{bmatrix}
            - & \overline{v}_{1} & - \\ 
            &\vdots& \\
            - & \overline{v}_{n} & - 
        \end{bmatrix}
        \begin{bmatrix}
            v_{1i} \\ \vdots \\ v_{ni}
        \end{bmatrix}
        =
        \begin{bmatrix}
            \sum_{j=1}^{m} \overline{v}_{j1}v_{ji} \\
            \sum_{j=1}^{m} \overline{v}_{j2}v_{ji} \\
            \vdots \\
            \sum_{j=1}^{m} \overline{v}_{jn}v_{ji}
        \end{bmatrix}
    \end{gather}
    Hvilket ifølge definitionen af indre produkt \ref{def:proj} betyder 
    \begin{gather}
        A^* v_i = 
        \begin{bmatrix}
            \langle v_i, v_1 \rangle \\
            \langle v_i, v_2 \rangle \\
            \vdots \\
            \langle v_i, v_n \rangle
        \end{bmatrix}
    \end{gather}
    Hvilket betyder, at:
    \begin{gather}
        A^*A = 
        \begin{bmatrix}
            \langle v_1, v_1 \rangle & \langle v_2, v_1 \rangle & \cdots & \langle v_n, v_1 \rangle \\
            \langle v_1, v_2 \rangle & \langle v_2, v_2 \rangle & \cdots & \langle v_n, v_2 \rangle \\
            \vdots & \vdots & \ddots & \vdots \\
            \langle v_1, v_n \rangle & \langle v_2, v_n \rangle & \cdots & \langle v_n, v_n \rangle
        \end{bmatrix}
    \end{gather}
    Derfor, hvis \( m \geq n \), så gælder det, at \( A^*A = D \), da hvis \( m < n \), ville $v_1, v_2 \cdots v_n$ ikke være lineært uafhængige vektorer.
\end{proof}
\end{theorem}

Derudover skal vi også undersøge, om spannet før og efter Gram-Schmidt processen forbliver det samme. Dette gøres ved brug af sætning \ref{span_theorem}.

\vspace{0.5cm}
% \begin{theorem}
%     Lad 
%     \begin{gather*}
%         W = [\mathbf{w}_1, \mathbf{w}_2, \ldots, \mathbf{w}_n], \quad \mathbf{w}_k \in \mathbb{F}^m \text{ for } k = 1, \ldots, n
%     \end{gather*}
%     hvor søjlerne af \( W \) er de resulterende vektorer efter at have udført Gram-Schmidt processen uden normalisering på 
%     \begin{gather*}
%         V = [\mathbf{v}_1, \mathbf{v}_2, \ldots, \mathbf{v}_n], \quad \mathbf{v}_k \in \mathbb{F}^m \text{ for } k = 1, \ldots, n.
%     \end{gather*}
%     Da gælder det, at \( \text{span}(V) = \text{span}(W) \). Dette kan undersøges ved at verificere, at
    
%     \[ \Phi([V \; W]) = [\Phi(V) \; \Phi(W)] \]
%     og 
%     \[\Theta([W \; V]) = [\Theta(W) \; \Theta(V)] \]

%     Hvor $\Phi : \mathbb{F}^{m \times s} \to \mathbb{F}^{m \times s}$ er den afbildning, som bringer $V$ i Række-echelon form (ref), dvs.
    
%     \[
%         \Phi(V) = 
%         \begin{bmatrix}
%             1 & \varphi_{1,2} & \varphi_{1,3} & \cdots \\
%             0 & 1 & \varphi_{2,3} & \cdots \\
%             0 & 0 & 1 & \cdots \\
%             0 & 0 & 0 & \ddots\\
%             \vdots & \vdots & \vdots  \\
%             0 & 0 & 0 & 
%         \end{bmatrix} \in \mathbb{F}^{m \times n}
%     \]

%     Samt $\Theta : \mathbb{F}^{m \times s} \to \mathbb{F}^{m \times s}$ er den afbildning, som bringer $W$ i Række-echelon form (ref).

%     Medføre at $\Phi(W)$ og $\Theta(V)$ er en øvre trekantsmatrix med 1 taller i diagonalerne.
% \end{theorem}
% \begin{proof}
%     Hvis \( W \subseteq V \), gælder det, at \(\text{span}(W) \subseteq \text{span}(V)\). Hvis derudover \( V \subseteq W \), så medfører det, at \(\text{span}(V) = \text{span}(W)\)\footcitetitle[Theorem 1.4.10 s. 25]{VLA}. 

%     \( W \) tilhører \(\text{span}(V)\), hvis \( \mathbf{w}_k \) for \( k = 1, \ldots, n \) er en linearkombination af vektorerne i \( V \). Da \(\mathbf{w}_k\) er resultatet af Gram-Schmidt processen uden normalisering, gælder det, at 
%     \[
%     \mathbf{w}_1 = \mathbf{v}_1
%     \]
%     og 
%     \[
%     \mathbf{w}_k = \mathbf{v}_k - \sum_{j=1}^{k-1} \text{proj}_{\mathbf{w}_j}(\mathbf{v}_k) = \mathbf{v}_k - \sum_{j=1}^{k-1} \frac{\langle \mathbf{v}_k, \mathbf{w}_j \rangle}{\langle \mathbf{w}_j, \mathbf{w}_j \rangle} \mathbf{w}_j \quad \text{for } k = 2, \ldots, n.
%     \]
%     Ved at anvende Gram-Schmidt processen på vektorerne i \( V \) fås:
%     \[
%     \begin{cases}
%         \mathbf{w}_1 = \mathbf{v}_1, \\
%         \mathbf{w}_2 = \mathbf{v}_2 - c_{2,1} \mathbf{w}_1 = \mathbf{v}_2 - c_{2,1} \mathbf{v}_1, \\
%         \mathbf{w}_3 = \mathbf{v}_3 - (c_{3,2} \mathbf{w}_2 + c_{3,1} \mathbf{w}_1) = \mathbf{v}_3 - (c_{3,2} \mathbf{v}_2 + (c_{3,1} - c_{3,2} c_{2,1}) \mathbf{v}_1), \\
%         \quad \vdots \\
%     \end{cases}
%     \]
%     hvilket betyder, at \( W \) er en linearkombination af \( V \) på formen:
%     \[
%     \mathbf{w}_k = \mathbf{v}_k - \sum_{j=1}^{k-1} \alpha_{k,j} \mathbf{v}_j, \quad k = 1, \ldots, n.
%     \]
    
%     $\Phi$  udfører en serie af rækkeoperationer som og er derfor lineær i rækkerne. Dermed gælder det at:
%     \[ \Phi(w_k) = \Phi(v_k - \sum_{j=1}^{k-1} \alpha_{k,j} v_j) = \Phi(v_k) - \sum_{j=1}^{k-1} \alpha_{k,j} \Phi(v_j), \quad k = 1, \ldots, n. \]
%     Hvor 
%     \[ \Phi(v_k) = 
%         \begin{bmatrix}
%             \varphi_{k, 1} \\
%             \vdots \\
%             \varphi_{k, k - 1} \\
%             1 \\
%             0 \\
%             \vdots
%         \end{bmatrix}, \quad
%         \text{For} \quad k = 2 \ldots n \quad \text{og} \quad
%         \Phi(v_1) =
%         \begin{bmatrix}
%             1 \\
%             0 \\
%             \vdots
%         \end{bmatrix}
%     \]
%     Derudover da $\sum_{j=1}^{k-1} \alpha_{k,j} \Phi(v_j)$ er summen af skalerede søjler $1$ til $k-1$ i $\Phi(V)$, gælder det at 
%     \[ \sum_{j=1}^{k-1} \alpha_{k,j} \Phi(v_j) = 
%         \begin{bmatrix}
%             \beta_{k, 1} \\
%             \vdots \\
%             \beta_{k, k - 1} \\
%             0 \\
%             \vdots
%         \end{bmatrix}
%     \]

%     \[
%     \Phi\left( \begin{bmatrix} V & W \end{bmatrix} \right) = 
%         \begin{bmatrix}
%             A \\ 
%             B
%         \end{bmatrix}
%     \]
%     Hvor $B \in M_{m-n, 2n}$ er en nulmatrix og 
%     \[ A = 
%     \begin{bmatrix}
%         1 & \varphi_{1,2} & \varphi_{1,3} & \cdots & \varphi_{1,n} & 1 & \psi_{1,2} & \psi_{1,3} & \cdots & \psi_{1,n} \\
%         0 & 1 & \varphi_{2,3} & \cdots & \varphi_{2,n} & 0 & 1 & \psi_{2,3} & \cdots & \psi_{2,n} \\
%         0 & 0 & 1 & \cdots & \varphi_{3,n} & 0 & 0 & 1 & \cdots & \psi_{3,n} \\
%         \vdots & \vdots & \vdots & \ddots & \vdots & \vdots & \vdots & \vdots & \ddots & \vdots \\
%         0 & 0 & 0 & \cdots & 1 & 0 & 0 & 0 & \cdots & 1 \\ 
%     \end{bmatrix}
%     \]
%     Da $A_{n+1.. 2n}$ danner en øvre trekantsmatrix, betyder det at $ W \subseteq V$ og dermed at $\text{span}(W) \subseteq \text{span}(V)$. Men yderemere eftersom $A_{n+1.. 2n}$ også har 1'ere i diagonalen, betyder det at vi kan bruge den samme serie af rækker operationer som bruges til at bringe $V$ i række-echelon form til at bringe $W$ i række-echelon form. Derfor gælder det at
%     \[
%     \Phi([W \; V]) = [\Phi(W) \; \Phi(V)]   
%     \]
%     Hvilket betyder at $V \subseteq W$ og dermed gælder det at $\text{span}(V) = \text{span}(W)$.   
% \end{proof}

\vspace{0.5cm}
\begin{theorem}\label{span_theorem}
    Lad 
    \begin{gather*}
        W = [\mathbf{w}_1, \mathbf{w}_2, \ldots, \mathbf{w}_n], \quad \mathbf{w}_k \in \mathbb{F}^m \text{ for } k = 1, \ldots, n
    \end{gather*}
    hvor søjlerne af \( W \) er de resulterende vektorer efter at have udført Gram-Schmidt processen uden normalisering på 
    \begin{gather*}
        V = [\mathbf{v}_1, \mathbf{v}_2, \ldots, \mathbf{v}_n], \quad \mathbf{v}_k \in \mathbb{F}^m \text{ for } k = 1, \ldots, n.
    \end{gather*}
    Da gælder det, at \( \text{span}(V) = \text{span}(W) \). Dette kan undersøges ved at verificere, at
    
    \[ \Phi([V \; W]) = [\Phi(V) \; \Phi(W)] \]
    og 
    \[\Theta([W \; V]) = [\Theta(W) \; \Theta(V)] \]

    hvor \(\Phi : \mathbb{F}^{m \times s} \to \mathbb{F}^{m \times s}\) er den afbildning, som bringer \(V\) i række-echelon form, dvs.
    
    \[
        \Phi(V) = 
        \begin{bmatrix}
            1 & \varphi_{1,2} & \varphi_{1,3} & \cdots \\
            0 & 1 & \varphi_{2,3} & \cdots \\
            0 & 0 & 1 & \cdots \\
            0 & 0 & 0 & \ddots\\
            \vdots & \vdots & \vdots  \\
            0 & 0 & 0 & 
        \end{bmatrix} \in \mathbb{F}^{m \times n}
    \]

    samt \(\Theta : \mathbb{F}^{m \times s} \to \mathbb{F}^{m \times s}\) er den afbildning, som bringer \(W\) i række-echelon form.

    Dette medfører, at \(\Phi(W)\) og \(\Theta(V)\) er øvre trekantsmatricer med 1-taller i diagonalerne.
\end{theorem}

\begin{proof}
    Hvis \( W \subseteq V \), gælder det, at \(\text{span}(W) \subseteq \text{span}(V)\). Hvis derudover \( V \subseteq W \), så medfører det, at \(\text{span}(V) = \text{span}(W)\)\footcitetitle[Theorem 1.4.10 s. 25]{VLA}. 

    \( W \) tilhører \(\text{span}(V)\), hvis \( \mathbf{w}_k \) for \( k = 1, \ldots, n \) er en linearkombination af vektorerne i \( V \). Da \(\mathbf{w}_k\) er resultatet af Gram-Schmidt processen uden normalisering, gælder det, at 
    \[
    \mathbf{w}_1 = \mathbf{v}_1
    \]
    og 
    \[
    \mathbf{w}_k = \mathbf{v}_k - \sum_{j=1}^{k-1} \text{proj}_{\mathbf{w}_j}(\mathbf{v}_k) = \mathbf{v}_k - \sum_{j=1}^{k-1} \frac{\langle \mathbf{v}_k, \mathbf{w}_j \rangle}{\langle \mathbf{w}_j, \mathbf{w}_j \rangle} \mathbf{w}_j \quad \text{for } k = 2, \ldots, n.
    \]
    Ved at anvende Gram-Schmidt processen på vektorerne i \( V \) fås:
    \[
    \begin{cases}
        \mathbf{w}_1 = \mathbf{v}_1, \\
        \mathbf{w}_2 = \mathbf{v}_2 - c_{2,1} \mathbf{w}_1 = \mathbf{v}_2 - c_{2,1} \mathbf{v}_1, \\
        \mathbf{w}_3 = \mathbf{v}_3 - (c_{3,2} \mathbf{w}_2 + c_{3,1} \mathbf{w}_1) = \mathbf{v}_3 - (c_{3,2} \mathbf{v}_2 + (c_{3,1} - c_{3,2} c_{2,1}) \mathbf{v}_1), \\
        \quad \vdots \\
    \end{cases}
    \]
    hvilket betyder, at \( W \) er en linearkombination af \( V \) på formen:
    \[
    \mathbf{w}_k = \mathbf{v}_k - \sum_{j=1}^{k-1} \alpha_{k,j} \mathbf{v}_j, \quad k = 1, \ldots, n.
    \]
    
    \(\Phi\) udfører en serie af rækkeoperationer og er derfor lineær i rækkerne. Dermed gælder det, at:
    \[ \Phi(\mathbf{w}_k) = \Phi(\mathbf{v}_k - \sum_{j=1}^{k-1} \alpha_{k,j} \mathbf{v}_j) = \Phi(\mathbf{v}_k) - \sum_{j=1}^{k-1} \alpha_{k,j} \Phi(\mathbf{v}_j), \quad k = 1, \ldots, n. \]
    hvor 
    \[ \Phi(\mathbf{v}_k) = 
        \begin{bmatrix}
            \varphi_{k, 1} \\
            \vdots \\
            \varphi_{k, k - 1} \\
            1 \\
            0 \\
            \vdots
        \end{bmatrix}, \quad
        \text{for} \quad k = 2 \ldots n \quad \text{og} \quad
        \Phi(\mathbf{v}_1) =
        \begin{bmatrix}
            1 \\
            0 \\
            \vdots
        \end{bmatrix}
    \]
    Derudover, da \(\sum_{j=1}^{k-1} \alpha_{k,j} \Phi(\mathbf{v}_j)\) er summen af skalerede søjler 1 til \(k-1\) i \(\Phi(V)\), gælder det, at 
    \[ \sum_{j=1}^{k-1} \alpha_{k,j} \Phi(\mathbf{v}_j) = 
        \begin{bmatrix}
            \beta_{k, 1} \\
            \vdots \\
            \beta_{k, k - 1} \\
            0 \\
            \vdots
        \end{bmatrix}
    \]

    \[
    \Phi\left( \begin{bmatrix} V & W \end{bmatrix} \right) = 
        \begin{bmatrix}
            A \\ 
            B
        \end{bmatrix}
    \]
    hvor \(B \in \mathbb{F}^{(m-n) \times 2n}\) er en nulmatrix og 
    \[ A = 
    \begin{bmatrix}
        1 & \varphi_{1,2} & \varphi_{1,3} & \cdots & \varphi_{1,n} & 1 & \psi_{1,2} & \psi_{1,3} & \cdots & \psi_{1,n} \\
        0 & 1 & \varphi_{2,3} & \cdots & \varphi_{2,n} & 0 & 1 & \psi_{2,3} & \cdots & \psi_{2,n} \\
        0 & 0 & 1 & \cdots & \varphi_{3,n} & 0 & 0 & 1 & \cdots & \psi_{3,n} \\
        \vdots & \vdots & \vdots & \ddots & \vdots & \vdots & \vdots & \vdots & \ddots & \vdots \\
        0 & 0 & 0 & \cdots & 1 & 0 & 0 & 0 & \cdots & 1 \\ 
    \end{bmatrix}
    \]
    Da \(A_{n+1..2n}\) danner en øvre trekantsmatrix, betyder det, at \( W \subseteq V\) og dermed, at \(\text{span}(W) \subseteq \text{span}(V)\). Men ydermere, eftersom \(A_{n+1..2n}\) også har 1'ere i diagonalen, betyder det, at vi kan bruge den samme serie af rækkeoperationer, som bruges til at bringe \(V\) i række-echelon form, til at bringe \(W\) i række-echelon form. Derfor gælder det, at
    \[
    \Phi([W \; V]) = [\Phi(W) \; \Phi(V)]   
    \]
    Hvilket betyder, at \(V \subseteq W\) og dermed gælder det, at \(\text{span}(V) = \text{span}(W)\).   
\end{proof}


I beviset til sætning \ref{span_theorem} viste vi at man vil kunne bruge de samme række operationer til at bringe \(W\) og \(V\) i række-echelon form. Dog da vi ikke gemmer rækkeoperation i vores implamentation vil vi nøjes med at lave en funktion \textcolor{red}{hasSameSpanGS} i Listing \ref{same_span}, som undersøger om at \(\Phi(W)\) og \(\Theta(V)\) er øvre trekantsmatricer med 1-taller i diagonalerne. 

\begin{lstlisting}[
    language={FSharp}, 
    label={same_span},
    caption={Funktion til at undersøge om søjlerne i to matricer har samme span}
    ]
// hasSameSpan : Matrix -> Matrix -> bool
let hasSameSpanGS mV mW = 
    let (D(r, c)) = dimMatrix mV
    if (D(r, c)) <> dimMatrix mW then failwith "Matrices must have the same dimension"
    else
    
    let Phi m1 m2 = 
        let s1, s2 = 
            extendMatrixWithMatrix m1 m2 
            |> rowEchelonForm 
            |> splitMatrix C (c-1)
        isUpperTriangularDiagonal1 s1 && isUpperTriangularDiagonal1 s2

    Phi mV mW && Phi mW mV
\end{lstlisting}

Dermed kan vi opskrive en en PBT af Gram-Schmidt processen i Listing \ref{pbt_gram_schmidt}.


\begin{lstlisting}[
    language={FSharp}, 
    label={pbt_gram_schmidt}, 
    caption={PBT af Gram-Schmidt processen}
    ]
let gramSchmidtIsOrthogonal (m:independentBacisMatrix) =
    let res =
        try 
            let um = orthogonalBacis m
            if  isOrthogonalBacis um && hasSameSpanGS m um then 
                1 
            else 
                0
        with
            | :? System.DivideByZeroException as _ -> 2
            | :? System.OverflowException as _ -> 3

    (res = 1 || res = 2 || res = 3)
    |> Prop.classify (res = 1) "PropertyHolds"
    |> Prop.classify (res = 2) "DivideByZeroExceptions"
    |> Prop.classify (res = 3) "OverflowException"
\end{lstlisting}

\begin{lstlisting}[
    style=output,
    label = {output_gram_schmidt},
    caption = {Output fra PBT af Gram-Schmidt processen}
]
> Arb.register<independentBacisMatrixGen>()
- let _ = Check.Quick gramSchmidtIsOrthogonal;;
Ok, passed 100 tests.
69% PropertyHolds.
31% OverflowException.
\end{lstlisting}

Outputtet fra PBT af Gram-Schmidt processen kan ses i Listing \ref{output_gram_schmidt}. Som sædvanligt indikerer testen kun korrekthed, men medfører ikke garanti for det.

Som nævnt i sektion \ref{sec:gram_schmidt} var der en slåfejl i Algorithm 9 for echelonform i noterne til "01001 Mathematics 1a"\footcitetitle{mat1a}. I forbindelse med at finde frem til denne fejl blev der lavet en PBT af echelonform af generatoren for lineært uafhængige vektorsæt. Egenskaben i \ref{eg:independtent} skal sikre, at både generatorens implementering er korrekt, og at funktionen til at finde echelonformen er det.

\vspace{0.5cm}
\begin{egenskab}\label{eg:independtent}
    Lad \( A \) være en matrix, hvor søjlerne er lineært uafhængige. Da gælder det, at \( A \) har fuld rank.
\end{egenskab}

Vi kan dermed opskrive en PBT af generatoren i Listing \ref{pbt_echelon_form}.

\begin{lstlisting}[
    language={FSharp}, 
    label={pbt_echelon_form}, 
    caption={PBT af generatoren for lineært uafhængige vektorsæt}
    ]
// independentBacisMatrixHasFullRank : independentBacisMatrix -> bool
let independentBacisMatrixHasFullRank (m:independentBacisMatrix) =
    let res =
        try 
            if hasFullRank m then 1 else 0
        with
            | :? System.OverflowException -> 2
    (res = 1 || res = 2)
    |> Prop.classify (res = 1) "PropertyHolds"
    |> Prop.classify (res = 2) "OverflowException"
\end{lstlisting}

Outputtet fra PBT af generatoren for lineært uafhængige vektorsæt kan ses i Listing \ref{output_echelon_form}. Testen viser, at egenskaben holder.

\begin{lstlisting}[
    style=output,
    label = {output_echelon_form},
    caption = {Output fra PBT af generatoren for lineært uafhængige vektorsæt}
]
> Arb.register<independentBacisMatrixGen>()
- let _ = Check.Quick independentBacisMatrixHasFullRank;;
Ok, passed 100 tests.
87% PropertyHolds.
13% OverflowException.
\end{lstlisting}
