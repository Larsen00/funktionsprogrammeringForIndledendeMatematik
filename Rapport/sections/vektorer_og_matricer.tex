Vi vil nu betragte et modul for vektorer og matricer. Da en Vector også kan betragtes som en matrix, vil vi herfra når der omtalles begge kun referare til en matrix. For at kunne håndtere matricer på en systematik måde begynder vi med at defienre en type for major order.

Vi vil nu betragte opbyggelsen af et modul for vektorer og matricer. Eftersom en vektor også kan opfattes som en matrix, vil vi i det følgende, når begge dele omtales, udelukkende referere til matricer. For systematik at kunne håndtere matricer, starter vi med at definere en type for lagringsordning.

\begin{lstlisting}[
    language={FSharp}, 
    label={Order}, 
    caption={Typen for order}
    ]
type Order = | R | C
\end{lstlisting}

Typen Order (se Listing \ref{Order}), anvendes til at angive, om en matrix er i rækkefølge (row-major) eller kolonnefølge (column-major)\footcitetitle{major_order}. En vektor, der er lagret i kolonnefølge, kan betragtes som den transponeret rækkefølge vektor. Vi kan derfor nu definere en type for matricer, ved hjælp af en type for vektore (Listing \ref{matrix}).

\begin{lstlisting}[
    language={FSharp}, 
    label={matrix}, 
    caption={Typen for Matricer}
    ]
type Vector = V of list<Number> * Order
type Matrix = M of list<Vector> * Order
\end{lstlisting}

Derudover er det en fordel at kunne kende dimissionen af en matrix. Derfor er der også defineret en type for dimissionen(se Listing \ref{dim}).

\begin{lstlisting}[
    language={FSharp}, 
    label={dim}, 
    caption={Typen for dimissionen}
    ]
// Rows x Cols
type Dimension = D of int * int
\end{lstlisting}



.-\subsection{Matrix operationer}
Der vil i denne sektion beskrives en række funktioner som er nødvendige før vi kan betragte nogle rekursive algoritmer som kan anvendes på en matrice. Da modulet indeholder mange hjælpe funktioner, vil der fokuseres på de funktioner med matematisk relevans.

Det muligt at definere en funktion til at finde dimissionen af en matrix (se Listing \ref{dim_func}). Funktionen laver et kald til \textcolor{red}{matrixValidMajor} genere en fejl hvis ikke alle vektorer og matrien har samme lagringsordning. \textcolor{red}{matrixVectorLength} finder længden af en vektor i matricen.

\begin{lstlisting}[
    language={FSharp}, 
    label={dim_func}, 
    caption={Funktion til at finde dimissionen af en matrix}
    ]
    // dimMatrix : Matrix -> Dimension
let dimMatrix (M(vl, o)) =
    if vl = [] then D (0, 0)
    else
    let _ = matrixValidMajor (M(vl, o))
    let d1 = List.length vl
    let d2 = matrixVectorLength (M(vl, o))
    match o with
    | R -> D (d1, d2)
    | C -> D (d2, d1)
\end{lstlisting}

Hvis en matrix er gemt som rækkefølge, vil antallet af rækker være længden af en vektor og antallet af kolonner være længden af vektor listen, og omvendt for kolonnefølge.

\subsubsection{Matematiske operationer}
I denne section bør bemærkes flere Listings ikke anvender fejlhåndtering, dette er udelukket for læsbarhedens skyld. De anvendte funktioner i mondulet har passende dimensions tjek på matricerne se evt. appendiks \ref{sec:matrix.fs}, der sikre at operationerne altid er lovlige.

\subsubsubsection{Skalering af en matrix}
Vi begynder med at betragte en funktion til at skalere en matrix (se Listing \ref{scale_matrix}). 

\begin{lstlisting}[
    language={FSharp}, 
    label={scale_matrix}, 
    caption={Funktion til at skalere en matrix}
    ]
// scalarVector : Number -> Vector -> Vector
let scalarVector (n:Number) (V (nl, o)) = 
    V ((List.map (fun x -> x * n) nl), o)

// scalarMatrix : Matrix -> Number -> Matrix
let scalarMatrix (M (vl, o)) n = 
    M ((List.map (fun x -> scalarVector n x) vl), o)
\end{lstlisting}

Det at skalere en matrice er svare til at skalere hvert element i matricen. Derfor ved at have en funktion \textcolor{red}{scalarVector}, der skalere hvert element i en givet vektor bliver \textcolor{red}{scalarMatrix} at skalere hver vektor i en givet matrice. List.\textcolor{red}{map} svare til at lave en list comprehension i Python\footcitetitle{list_comprehension}.


\begin{lstlisting}[
    language={FSharp}, 
    label={add_matrix}, 
    caption={Funktion til at addere matricer og substraktion af vektorer}
    ]
// addVector : Vector -> Vector -> Vector
let addVector (V (v1, o1)) (V (v2, _)) =
    V ((List.map2 (+) v1 v2), o1)

// addMatrix : Matrix -> Matrix -> Matrix
let addMatrix (M(vl1, o)) (M(vl2, _)) =
    M (List.map2 addVector vl1 vl2, o)

// subVector : Vector -> Vector -> Vector
let subVector x y =
    scalarVector (-one) y |> addVector x
    
// sumRows : Matrix -> Matrix
let rec sumRows m = 
    if not <| corectOrderCheck m C 
    then sumRows <| correctOrder m C
    else
    let zeroVector = vectorOf zero <| matrixVectorLength m
    let (M(vl, _)) = m
    matrix [List.fold (addVector) zeroVector vl]
\end{lstlisting}

\subsubsubsection{Addition af matricer}
Addition af vektorer kommer ned til at fortage additionen elementvis i Listing \ref{add_matrix}, ved brug af List funktionen \textcolor{red}{map2}. Vi kan bruge \textcolor{red}{addVector} til at definere, matrix addition og subtraktion af vektorer sidst bruges den også til at summere rækkerne i en matrice (\textcolor{red}{sumRows}) som vil blive brugt i implementeringen af Gram-Schmidt processen i sektion \ref{sec:gram_schmidt}, funktionen bliver yderligere beskrevet i defination \ref{sumRows}.
\vspace{0.5cm}
\begin{definition}[Summering af rækker i en matrix] \label{sumRows}
Lad $A$ være en matrix med $m$ rækker og $n$ søjler, hvor
\[
A = \begin{bmatrix}
a_{11} & a_{12} & \cdots & a_{1n} \\
a_{21} & a_{22} & \cdots & a_{2n} \\
\vdots & \vdots & \ddots & \vdots \\
a_{m1} & a_{m2} & \cdots & a_{mn}
\end{bmatrix}
\]

Så gælder om \textcolor{red}{sumRows} at

\[
\textit{\textcolor{red}{sumRows}}(A) =
v = \begin{bmatrix}
\sum_{j=1}^{n} a_{1j} \\
\sum_{j=1}^{n} a_{2j} \\
\vdots \\
\sum_{j=1}^{n} a_{mj}
\end{bmatrix}
\]

Dermed er $v_i = \sum_{j=1}^{n} a_{ij}$ for $i = 1, 2, \ldots, m$.
\end{definition}

\subsubsubsection{Matrix multiplikation}


\begin{definition}[Matrix-Vektor Multiplikation] \label{def:matrix_vector_mul}
    Lad $A$ være en matrix med $m$ rækker og $n$ søjler, hvor
    \[
    A = \begin{bmatrix}
    a_{11} & a_{12} & \cdots & a_{1n} \\
    a_{21} & a_{22} & \cdots & a_{2n} \\
    \vdots & \vdots & \ddots & \vdots \\
    a_{m1} & a_{m2} & \cdots & a_{mn}
    \end{bmatrix}
    \]
    og $\mathbf{v} = (v_1, v_2, \ldots, v_n)^T$ være en vektor med $n$ elementer. Så er matrix-vektor $Av$ produktet defineret som
    \[
\begin{bmatrix}
    a_{11} & a_{12} & \cdots & a_{1n} \\
    a_{21} & a_{22} & \cdots & a_{2n} \\
    \vdots & \vdots & \ddots & \vdots \\
    a_{m1} & a_{m2} & \cdots & a_{mn}
\end{bmatrix}
\begin{bmatrix}
    v_{1} \\
    v_{2} \\
    \vdots \\
    v_{n}
\end{bmatrix}
=
\begin{bmatrix}
    a_{11}v_{1} + a_{12}v_{2} + \cdots + a_{1n}v_{n} \\
    a_{21}v_{1} + a_{22}v_{2} + \cdots + a_{2n}v_{n} \\
    \vdots \\
    a_{m1}v_{1} + a_{m2}v_{2} + \cdots + a_{mn}v_{n}
\end{bmatrix}
= \begin{bmatrix}
    \sum_{j=1}^{n} a_{1j}v_{j} \\
    \sum_{j=1}^{n} a_{2j}v_{j} \\
    \vdots \\
    \sum_{j=1}^{n} a_{mj}v_{j}
\end{bmatrix}
\]
\end{definition}

\begin{theorem}[Matrix-Vektor Multiplikation] \label{matrix_vector_mul}
    Lad $A$ være en matrix med $m$ rækker og $n$ søjler, og lad $\mathbf{v}$ være en vektor med $n$ elementer. Så gælder der
    \[ Av = \textit{\textcolor{red}{sumRows}}
    \begin{bmatrix}
        | & | &        & | \\
        a_{1}v_{1} & a_{2}v_{2} & \cdots & a_{n}v_{n} \\
        | & | &        & | \\
    \end{bmatrix}
    \]
\end{theorem}
\begin{proof}
    Lad $B$ være resultatet af at skalere søjlerne i matrix $A$ med de tilsvarende elementer i vektoren $\mathbf{v}$. Vi har
    \begin{alignat*}{2}
    B &= \begin{bmatrix}
        | & | &        & | \\
        a_{1}v_{1} & a_{2}v_{2} & \cdots & a_{n}v_{n} \\
        | & | &        & | \\
    \end{bmatrix} \\
    &= \begin{bmatrix}
        a_{11}v_{1} & a_{12}v_{2} & \cdots & a_{1n}v_{n} \\
        a_{21}v_{1} & a_{22}v_{2} & \cdots & a_{2n}v_{n} \\
        \vdots & \vdots & \ddots & \vdots \\
        a_{m1}v_{1} & a_{m2}v_{2} & \cdots & a_{mn}v_{n}
    \end{bmatrix}
    \end{alignat*}
    Ved brug af definition \ref{sumRows} for \textit{\textcolor{red}{sumRows}} og definition \ref{def:matrix_vector_mul} ses det at 
    \begin{alignat*}{2}
    \text{{\color{red}\textit{sumRows}}}(B) &= \begin{bmatrix}
        \sum_{j=1}^{n} a_{1j}v_{j} \\
        \sum_{j=1}^{n} a_{2j}v_{j} \\
        \vdots \\
        \sum_{j=1}^{n} a_{mj}v_{j}
    \end{bmatrix} = A\mathbf{v}
    \end{alignat*}
    \end{proof}
Vi kan dermed anvende Sætning \ref{matrix_vector_mul} til at definere en funktion \textcolor{red}{matrixMulVector} for matrix-vektor multiplikation (se Listing \ref{mul_vector}). Som først skalere søjlerne i matricen med de tilsvarende element i vektoren, og derefter summe rækkerne i matricen. 
\begin{lstlisting}[
    language={FSharp}, 
    label={mul_vector}, 
    caption={Funktion til matrix-vektor multiplikation}
    ]
// matrixMulVector : Matrix -> Vector -> Matrix
let rec matrixMulVector (M(vl, _)) (V(nl, _)) =
    M (List.map2 (fun mc n -> scalarVector n mc) vl nl, C) 
    |> sumRows
\end{lstlisting}

\begin{definition}[Matrix multiplikation\footcitetitle{mat1a}]\label{def:matrix_matrix_mul}
    Lad $A \in \mathbb{F}^{m \times n}$ og $B \in \mathbb{F}^{n \times \ell}$. Antag at søjlerne i $B$ er givet ved $b_1, \ldots, b_\ell \in \mathbb{F}^n$, dermed
    \[
        B = \begin{bmatrix}
    | &  & | \\
    b_1 & \cdots & b_\ell \\
    | &  & | \\
\end{bmatrix}.
\]
Så defineres matrice produktet som
\[
    A \cdot B = \begin{bmatrix}
        | &  & | \\
        A \cdot b_1 & \cdots & A \cdot b_\ell \\
        | &  & | \\
    \end{bmatrix}.
    \]
\end{definition}
Udfra defination \ref{def:matrix_matrix_mul}, ses det at funktion \textcolor{red}{matrixProduct} i Listing \ref{mul_matrix} til at multiplicere to matricer, bliver derfor at lave matrix-vektor multiplikation på hver søjle i matricen. Da \textcolor{red}{matrixMulVector} returnere en matrix, skal der bruges en funktion til at konvertere matricen til en vektor, hvilket \textcolor{red}{matrixToVector} gør.
\begin{lstlisting}[
    language={FSharp}, 
    label={mul_matrix}, 
    caption={Funktion til at multiplicere matricer}
    ]
// matrixProduct : Matrix -> Matrix -> Matrix
let rec matrixProduct a (M(vlb, _)) =
    let product = List.map (
        fun bv -> matrixMulVector a bv |> matrixToVector ) vlb
    M(product, C)
\end{lstlisting}
    
\subsubsubsection{Projektion af en vektor}
Som beskrevet i afsnit 2.3 'Projections onto a line' i 'Mathematics 1b' \footcitetitle{mat1b}, kan projektionen af en vektor defineres som følgende, hvor $Y = \text{span}\{y\}$.
\begin{align}
    \text{proj}_Y : V \rightarrow V, \quad \text{proj}_Y(x) = \frac{\langle x, y \rangle}{\langle y, y \rangle} y
    \label{proj}
\end{align}
Med det standard indreprodukt 
\begin{align}
    \langle x, y \rangle = y^* x =\sum_{k=1}^{n} x_k \overline{y}_k
\end{align}

Den første funktion vi skal bruge er derfor en funktionen til at konjugere en vektor \textcolor{red}{conjugateVector}, det gøres ved at konjugere elementerne i vektoren. Udover dette defineres en funktion til at multiplikere to vektorer element vis \textcolor{red}{vectorMulElementWise}. 

\begin{lstlisting}[
    language={FSharp}, 
    label={proj_fun}, 
    caption={Funktioner til projektere en vektor på en anden}
    ]
// vectorMulElementWise : Vector -> Vector -> Vector
let vectorMulElementWise (V(u, o1)) (V(v, o2)) =
    V (List.map2 (*) u v, o1)

// conjugateVector : Vector -> Vector
let conjugateVector (V(v, o)) = 
    V (List.map conjugate v, o)

// innerProduct : Vector -> Vector -> Number
let innerProduct u v =
    let (V(w, _)) = conjugateVector v |> vectorMulElementWise u
    List.fold (+) zero w

// proj : Vector -> Vector -> Vector   
let proj y x =
    scalarVector (innerProduct x y / innerProduct y y) y
\end{lstlisting}

Evalueringen af det standard indre produkt \textcolor{red}{innerProduct} mellem to vektore, bliver derfor at konjugere den ene vektor og derefter multiplicere elementvis med den anden vektor. Hvortil summen af elementerne i den resulterende vektor er det indre produkt.

Sidst kan funktionen \textcolor{red}{proj} skrives direkte som den er defineres i ligning \ref{proj}.

\subsection{PBT af matrix operationer}
Det er nu muligt at opstille nogle PBT af der sikre at matricerne overholder matematiske egenskaber i sætning \ref{vector_space_axioms}. Først defineres en generator for matricer, som generere matricer med tilfældige tal fra vores talmængde \ref{number_type}.

\begin{lstlisting}[
    language={FSharp}, 
    label={generators}, 
    caption={Generatorene anvendt til PBT af matrix operationer i }
    ]
// vectorGen : int -> Gen<Vector>
let vectorGen n =
    Gen.listOfLength n numberGen |> Gen.map (fun x -> vector x)

// matrixGen : Gen<Matrix>
let matrixGen =
    gen {
        let! row = Gen.choose(1, 6)
        let! col = Gen.choose(1, 6)
        let! vectors = Gen.listOfLength col (vectorGen row)
        return matrix vectors
    }

type MaxtrixGen =
    static member Matrix() =
        {new Arbitrary<Matrix>() with
            override _.Generator = matrixGen
            override _.Shrinker _ = Seq.empty}

type NumberGen =
    static member Number() =
        {new Arbitrary<Number>() with
            override _.Generator = numberGen
            override _.Shrinker _ = Seq.empty}   
\end{lstlisting}
\begin{egenskab}[Vektor Aksiomer\footnote{theorem 7.2 i mat 1 noterne}]\label{vector_space_axioms}
    Lad $c, d \in \mathbb{F}$ og $v_i \in \mathbb{F}^n$ for $i = 1 \dots m$ så gælder:
    \begin{enumerate}
        \item $(v_1 + \dots + v_{m-1}) + v_m = v_1 + (v_2 + \dots + v_m)$
        \item $c \cdot \left(d \cdot \begin{bmatrix}
            | &        & | \\
            v_{1} & \cdots & v_{m} \\
            | &        & | \\
        \end{bmatrix}\right) = (c \cdot d) \cdot \begin{bmatrix}
            | &        & | \\
            v_{1} & \cdots & v_{m} \\
            | &        & | \\
        \end{bmatrix}$
        
        \item $c \cdot (v_1 + \dots +v_m) = c \cdot v_1 + \dots +c \cdot v_m$
    \end{enumerate}
\end{egenskab}

Vi kan dermed nu lave definere egenskaberne fra sætning \ref{vector_space_axioms} som nogle funktioner, og teste dem med PBT.

\begin{lstlisting}[
    language={FSharp}, 
    label={lst:vector_space_axioms}, 
    caption={Egenskaberne fra sætning \ref{vector_space_axioms} som funktioner}
    ]
vectorCom : Matrix -> bool
let vectorCom m =
    sumRows m = sumRows (flip m)

vectorScalarAss : Matrix -> Number -> Number -> bool
let vectorScalarAss (m:Matrix) (n1:Number) (n2:Number) =
    n1 * (n2 * m) = (n1 * n2) * m

vectorAssCom : Matrix -> Number -> bool
let vectorAssCom m (c:Number) =
    c * (sumRows m) = sumRows (c * m)
\end{lstlisting}

\begin{lstlisting}[
    style=output, 
    label={lst:vector_space_axioms_pbt}, 
    caption={Outputtet fra PBT af vektor Listing \ref{lst:vector_space_axioms}}
    ]
- Arb.register<MaxtrixGen>()
- Arb.register<NumberGen>()
- let _ = Check.Quick vectorCom
- let _ = Check.Quick vectorScalarAss
- let _ = Check.Quick vectorAssCom;;
Ok, passed 100 tests.
Ok, passed 100 tests.
Ok, passed 100 tests.
\end{lstlisting}

\subsection{Række-echelon form}
\subsection{Gram-Schmidt}\label{sec:gram_schmidt}
Vi kan nu betragte implementeringen af Gram-Schmidt processen. Denne proces kan anvendes rekursivt til at finde en ortonormal basis for et underrum udspændt af en liste af vektorer $v_1, v_2, \ldots, v_n$. Processen kan implementeres rekursivt idet de nye vektorer $w_k$ for $k = 2, 3, \ldots, n$ konstrueres baseret på alle de tidligere vektorer $w_1, \ldots, w_{k-1}$. 

Før vi implementerer Gram-Schmidt processen, er vi dog begrænset af vores Number type \ref{number_type}, idet $x \in \text{\{Number\}} \centernot\implies \sqrt{x} \in \text{\{Number\}}$. Derfor vil vi ikke normalisere vektorerne, hvilket medfører, at vi kun vil finde en ortogonal basis, fremfor en ortonormal basis.

\begin{lstlisting}[
    language={FSharp}, 
    label={gram_schmidt}, 
    caption={Dannelsen af en ortogonal basis, ved hjælp af Gram-Schmidt processen}
    ]
// orthogonalBacis : Matrix -> Matrix
let orthogonalBacis m =
    if not <| corectOrderCheck m C  
    then orthogonalBacis (correctOrder m C)
    else

    // Gram_Schmidt : Matrix -> (Vector list -> Matrix) -> Matrix
    let rec Gram_Schmidt vm acc_wm =
        match acc_wm [], vm with
        | x, M([], _) -> x 
        | M([], _), M(v1::vrest, o) -> 
            Gram_Schmidt (M(vrest,o)) 
            <| fun x -> extendMatrix (M([v1], C)) x 
        | M(w, _), M(vk::vrest, o) -> 
            let (V(wk, _)) = vk - sumProj w vk
            Gram_Schmidt (M(vrest,o)) 
            <| fun x -> extendMatrix (acc_wm wk) x

    // sumProj : Vector list -> Vector -> Vector
    and sumProj w vk =
        List.map (fun x -> proj x vk) w 
        |> matrix 
        |> sumRows
        |> matrixToVector
        
    Gram_Schmidt m (fun _ -> M([], C))
\end{lstlisting}


Funktionen \textcolor{red}{\texttt{sumProj}} tager en liste med vektorer \(w\), som i Gram-Schmidt-processen er de tidligere behandlede vektorer \(w_1, \ldots, w_{k-1}\), og en vektor \(v_k\) som er den \(k\)'te vektor. $v_k$ projiceres på alle vektorerne i \(w\), hvorefter der tages summen af disse projektioner.

Funktionen \textcolor{red}{Gram\_Schmidt}, tager en matrix hvor søjlerne er de vektores som ønskes at finde en ortogonal basis for. Der udover tager den en akkumulerende funktion som indeholder de behandlede vektorer. Hvis der ikke er flere vektorer i matricen, gives den akkumulerede funktion. Hvis der ikke er nogle vektorer i akkumulatoren, tages den første vektor fra matricen og tilføjes til akkumulatoren. Hvis der er vektorer i både akkumulatoren og matricen, kaldes \textcolor{red}{sumProj} på den akkumulerede liste og den første vektor i matricen. Resultatet trækkes fra den første vektor i matricen, og dette bliver den nye vektor som tilføjes til akkumulatoren. 

Funktionen \textcolor{red}{orthogonalBacis} tager en matrix og tjekker om matricen er i kolonnefølge, hvis ikke kalder funktionen sig selv, med den korrekte lagringsordning. Ellers kaldes \textcolor{red}{Gram\_Schmidt} med matricen og en tom akkumulator. Resultatet bliver derfor en matrix med en ortogonal basis for underrummet udspændt af de givne vektorer, givet at vektorerne er lineært uafhængige.

\subsection{PBT af Gram-Schmidt}
Udfrodringen ved at lave en PBT af Gram-Schmidt er at vektorsættet skal være lineært uafhængige. Derfor laves der en generator som ved at udføre tilfældige række operationer på en diagonal matrix, kan generere en matrix med lineært uafhængige vektorer. 
\#TODO : Lav et bevis for det beholder enskaben for lineært uafhængighed.

\begin{lstlisting}[
    language={FSharp}, 
    label={generators_gram_schmidt}, 
    caption={Generatorene anvendt til PBT af Gram-Schmidt}
    ]
// getBacismatrixGen : int -> Gen<Matrix>
let getBacismatrixGen n =
    Gen.map (fun x -> standardBacis x) (Gen.choose (2, n))

// performRowOperationGen : Matrix -> Gen<Matrix>
let performRowOperationGen m =
    let (D(n, _)) = dimMatrix m
    gen { 
        let! i = Gen.choose(1, n)
        let! j = match i with
            | 1 -> Gen.choose(2, n)
            | _ when i = n -> Gen.choose(1, n-1)
            | _ -> Gen.oneof [
                    Gen.choose(1, i-1); 
                    Gen.choose(i+1, n)]
        let! a = numberGen
        return rowOperation i j a m }


// multipleRowOperationsGen : Matrix -> int -> Gen<Matrix>
let rec multipleRowOperationsGen m count =
    if count <= 0 then Gen.constant m
    else
        gen {
            let! newMatrix = performRowOperationGen m
            return! multipleRowOperationsGen newMatrix (count - 1)
        }

// getIndependetBacisGen : Gen<Matrix>
let getIndependetBacisGen =
    gen { 
        let! m = getBacismatrixGen 5
        let! numberOfOperations = Gen.choose(1, 10)
        let! span = multipleRowOperationsGen m numberOfOperations
        return span }

type IndependetBacis = Matrix
type IndependetBacisGen =
    static member IndependetBacis() =
        {new Arbitrary<Matrix>() with
            override _.Generator = getIndependetBacisGen
            override _.Shrinker _ = Seq.empty}
\end{lstlisting}

Listing \ref{generators_gram_schmidt} viser de forskellige generatorer, som anvendes til PBT (Property-Based Testing) af Gram-Schmidt-processen. Først genereres en tilfældig basis matrix. Dernæst udvælges to tilfældige rækker, \(i\) og \(j\), hvorefter der udføres en rækkeoperation på \(R_j\), således at \(R_j \leftarrow R_j - aR_i\), hvor $a$ er et tilfældigt Number. Denne proces gentages et tilfældigt antal gange.

Dernæst skal vi bruge en funktion til at tjekke om en matrix er en ortogonal basis. \textcolor{red}{isOrthogonalBacis} i Listing \ref{check_orthogonal_basis} tjekker om alle vektorerne i en matrix er ortogonale, ved at tjekke om søjle $v_i$ er ortogonal med $v_{i+1}$, for alle $i \in [1, n-1]$ hvor $n$ er længden på søjlerne. To søjler er ortogonale hvis deres indreprodukt er 0.

\begin{lstlisting}[
    language={FSharp}, 
    label={check_orthogonal_basis}, 
    caption={Funktion til at tjekke om søjlerne i en matrix er en ortogonal basis}
    ]
// isOrthogonalBacis : Matrix -> bool
let rec isOrthogonalBacis (M(vl, o)) =
    if not <| corectOrderCheck (M(vl, o)) C 
    then isOrthogonalBacis <| correctOrder (M(vl, o)) C
    else
    match vl with
    | [] -> true
    | _::[] -> true
    | v::vnext::vrest -> innerProduct v vnext = zero && isOrthogonalBacis (M(vnext::vrest, o))
\end{lstlisting}
%TODO: ET bevis for denne kunne være nice

PB testen \textcolor{red}{gramSchmidtIsOrthogonal} bliver derfor blot at tjekke om en matrix bestående af lineært uafhængige vektorer, der udspænder et underrum, er orthogonale efter Gram-Schmidt processen er blevet anvendt. Grundet tilfældige matematiske operationer, opstår der en støre mængde opstå overflow fejl, derfor godtages disse men klassificeres som overflow.

\begin{lstlisting}[
    language={FSharp}, 
    label={pbt_gram_schmidt}, 
    caption={PBT af Gram-Schmidt processen}
    ]
let gramSchmidtIsOrthogonal (m:IndependetBacis) =
    let res =
        try 
            if orthogonalBacis m |> isOrthogonalBacis then 1 else 0
        with
            | :? System.OverflowException -> 2
    (res = 1 || res = 2)
    |> Prop.classify (res = 1) "PropertyHolds"
    |> Prop.classify (res = 2) "OverflowException"
\end{lstlisting}

\begin{lstlisting}[
    style=output,
    label = {output_gram_schmidt},
    caption = {Output fra PBT af Gram-Schmidt processen}
]
- Arb.register<IndependetBacisGen>()
- let _ = Check.Quick gramSchmidtIsOrthogonal;;
Ok, passed 100 tests.
69% PropertyHolds.
31% OverflowException.
\end{lstlisting}

Outputtet fra PBT af Gram-Schmidt processen kan ses i Listing \ref{output_gram_schmidt}. Som sædvanligt indikere testen kun korrekthed, men ikke garanteret korrekthed.
